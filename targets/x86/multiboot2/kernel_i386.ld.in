#include <ld/ldsym.h>
#include <config.h>

ENTRY(_start)

SECTIONS
{
	. = 1M;

	.multiboot2_header :
	{
		KEEP(*(.multiboot2_header))
	}

	. += 1; /* Avoiding some stupid bug in linker or something I don't understand.
		 Without this .multiboot2_header and .text sections appear to start
		 at the same address besides they are both non-empty sections. */

	.text : ALIGN(16)
	{
		*(.text_entry)
		*(.text)
		*(.text.*)
	}

	.rodata : ALIGN(4K)
	{
		*(.rodata)
		*(.rodata.*)
	}

	.ldsym : ALIGN(4K)
	{
		DEFINE_LDSYM(stack_top, 		__STACK_TOP)
		DEFINE_LDSYM(kernel_image_end_vma, 	0)
		DEFINE_LDSYM(kernel_image_end_lma, 	0)

		DEFINE_LDSYM(kernel_text_start_vma, 	__FAKE_KERNEL_MAIN)
		DEFINE_LDSYM(kernel_text_start_lma, 	__FAKE_KERNEL_MAIN)
		DEFINE_LDSYM(kernel_text_size, 		__FAKE_KERNEL_MAIN_TEXT_SIZE)

		DEFINE_LDSYM(kernel_bss_start_vma, 	0)
		DEFINE_LDSYM(kernel_bss_start_lma, 	0)
		DEFINE_LDSYM(kernel_bss_size, 		0)

		DEFINE_LDSYM(kernel_rodata_start_vma, 	0)
		DEFINE_LDSYM(kernel_rodata_start_lma, 	0)
		DEFINE_LDSYM(kernel_rodata_size, 	0)

		DEFINE_LDSYM(kernel_data_start_vma, 	0)
		DEFINE_LDSYM(kernel_data_start_lma, 	0)
		DEFINE_LDSYM(kernel_data_size, 		0)

		DEFINE_LDSYM(kernel_main, 		__FAKE_KERNEL_MAIN)
	}

	.data : ALIGN(4K)
	{
		*(.data)
	}

	.stack BLOCK(CONFIG_STACK_SIZE) (NOLOAD) : ALIGN(4K)
	{
		. += CONFIG_STACK_SIZE;
	}

	PROVIDE(__STACK_TOP = .);
	PROVIDE(__FAKE_KERNEL_MAIN = .);

	.fake_main_text :
	{
		KEEP(*(.fake_main_text))
	}

	PROVIDE(__FAKE_KERNEL_MAIN_TEXT_SIZE = . - __FAKE_KERNEL_MAIN);

	/DISCARD/ :
	{
		*(.bss) /* .bss won't be used in i386-specific code. */
		*(.note.*)
		*(.comment)
		*(.eh_frame)
	}
}
